apply plugin: 'com.android.application'

android {
    compileSdkVersion versions.compileSdk
    defaultConfig {
        applicationId "me.kuehle.carreport"
        minSdkVersion 15
        targetSdkVersion versions.targetSdk
        versionCode 74
        versionName "3.23.1"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }
    signingConfigs {
        release {
            keyAlias signing_key_alias
            keyPassword signing_key_password
            storeFile file(signing_key_store_file)
            storePassword signing_key_store_password
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    flavorDimensions "default"
    productFlavors {
        foss {
            dimension "default"
        }
        full {
            dimension "default"
        }
    }
    lintOptions {
        checkReleaseBuilds false
    }
    packagingOptions {
        exclude 'META-INF/LICENSE'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // 2018 style of testing
    useLibrary 'android.test.runner'
    useLibrary 'android.test.base'
}

repositories {
    google()
    jcenter()

    // Required for HelloCharts for Android
    maven { url "https://jitpack.io" }
}

dependencies {
    // Multidex
    implementation 'com.android.support:multidex:1.0.3'

    // Charts
    implementation 'com.github.gudatcomputers:hellocharts-android:ba603174d302931c0985afb4e54d597fba28146b'

    // CSV
    implementation 'org.apache.commons:commons-csv:1.5'

    // Dropbox
    implementation 'com.dropbox.core:dropbox-core-sdk:3.0.8'

    // Support, AppCompat
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha2'

    // Room Persistence
    def room_version = "2.1.0-alpha02"
    implementation "androidx.room:room-runtime:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"

    // Play Services: for Google Drive
    fullImplementation "com.google.android.gms:play-services-auth:${versions.playServices}"
    fullImplementation "com.google.android.gms:play-services-drive:${versions.playServices}"

    // The new Android Drive API, which is included in the play services, has caching bugs and does
    // not provide all functionality. Until this is fixed we have to use the old Google API.
    fullImplementation('com.google.oauth-client:google-oauth-client:1.22.0') {
        exclude group: 'org.apache.httpcomponents'
    }
    fullImplementation('com.google.api-client:google-api-client:1.22.0') {
        exclude group: 'org.apache.httpcomponents'
    }
    fullImplementation('com.google.apis:google-api-services-drive:v3-rev81-1.22.0') {
        exclude group: 'org.apache.httpcomponents'
    }
    fullImplementation('com.google.api-client:google-api-client-android:1.22.0') {
        exclude group: 'org.apache.httpcomponents'
    }
    fullImplementation('com.google.http-client:google-http-client-android:1.22.0') {
        exclude group: 'org.apache.httpcomponents'
    }

    // WebDAV
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'

    // UI Helpers
    implementation 'com.github.clans:fab:1.6.4'
    implementation 'com.bignerdranch.android:recyclerview-multiselect:0.2'
    implementation 'de.psdev.licensesdialog:licensesdialog:1.8.3'
    implementation 'uk.co.jordanrobinson:android-color-picker:1.0.2'

    // Joda-Time for Android
    implementation 'net.danlew:android.joda:2.9.9.4'

    // Required for instrumented tests
    androidTestImplementation 'androidx.annotation:annotation:1.0.0'
    androidTestImplementation 'androidx.test:runner:1.1.0'
}