apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"
    defaultConfig {
        applicationId "me.kuehle.carreport"
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 42
        versionName "3.8.0"
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources false // Currently this changes the size of resources from 1932KB to 1943KB, making it useless.
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
        foss
        full
    }
    packagingOptions {
        // These files are included in at least two of the following libraries:
        // joda-time-2.6.jar, httpmime-4.0.3.jar, httpcore-4.0.1.jar, commons-logging-1.1.1.jar
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/DEPENDENCIES'
    }
}

repositories {
    // Required for com.google.api-client
    mavenCentral()

    // Required for HelloCharts for Android
    maven { url "http://dl.bintray.com/gudatcomputers/maven" }
}

dependencies {
    // Charts
    compile 'com.github.lecho:hellocharts-library:1.5.9@aar'

    // Dropbox
    compile 'com.dropbox.core:dropbox-core-sdk:2.0-beta-2'

    // Support, AppCompat
    compile 'com.android.support:support-v4:23.1.1'
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:cardview-v7:23.1.1'
    compile 'com.android.support:recyclerview-v7:23.1.1'
    compile 'com.android.support:design:23.1.1'

    // Play Services: for Google Drive
    fullCompile 'com.google.android.gms:play-services-plus:8.3.0'
    fullCompile 'com.google.android.gms:play-services-drive:8.3.0'

    // The new Android Drive API, which is included in the play services, has caching bugs and does
    // not provide all functionality. Until this is fixed we have to use the old Google API.
    fullCompile 'com.google.apis:google-api-services-drive:v2-rev105-1.17.0-rc'
    fullCompile('com.google.api-client:google-api-client-android:1.17.0-rc') {
        exclude group: 'com.google.android.google-play-services'
    }
    fullCompile('com.google.http-client:google-http-client-gson:1.17.0-rc') {
        exclude module: 'httpclient'
    }

    // WebDAV
    compile 'org.apache.jackrabbit:jackrabbit-webdav:2.10.1'

    // UI Helpers
    compile 'com.larswerkman:HoloColorPicker:1.4'
    compile 'com.github.clans:fab:1.6.1'
    compile 'com.bignerdranch.android:recyclerview-multiselect:0.2'

    // Joda-Time for Android
    compile 'net.danlew:android.joda:2.9.0'
}