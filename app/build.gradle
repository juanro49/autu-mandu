apply plugin: 'com.android.application'

android {
    compileSdkVersion versions.compileSdk
    buildToolsVersion versions.buildTools
    namespace 'org.juanro.autumandu'

    defaultConfig {
        applicationId "org.juanro.autumandu"
        minSdkVersion 21
        targetSdk versions.targetSdk
        versionCode 403
        versionName "4.0.3"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    signingConfigs {
        release {
            keyAlias signing_key_alias
            keyPassword signing_key_password
            storeFile file(signing_key_store_file)
            storePassword signing_key_store_password
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    buildFeatures {
        buildConfig = true
    }

    flavorDimensions "default"
    productFlavors {
        foss {
            dimension "default"
        }
    }

    lint {
        checkReleaseBuilds false
    }

    packagingOptions {
        resources {
            excludes += ['META-INF/LICENSE']
        }
    }

    //compileOptions {
    //    sourceCompatibility JavaVersion.VERSION_1_8
    //    targetCompatibility JavaVersion.VERSION_1_8
    //}

    // 2018 style of testing
    useLibrary 'android.test.runner'
    useLibrary 'android.test.base'
}

dependencies {
    // Multidex
    implementation 'com.android.support:multidex:1.0.3'

    // Charts
    implementation 'com.github.gudatcomputers:hellocharts-android:ba603174d302931c0985afb4e54d597fba28146b'

    // CSV
    implementation 'org.apache.commons:commons-csv:1.12.0'

    // Filesystem tools
    implementation 'androidx.documentfile:documentfile:1.1.0-alpha01'

    // Support, AppCompat
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.4.0-rc01'
    implementation 'com.google.android.material:material:1.13.0-alpha07'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.0'

    // Room Persistence
    implementation 'androidx.room:room-runtime:2.7.0-alpha11'
    annotationProcessor 'androidx.room:room-compiler:2.7.0-alpha11'

    // WebDAV
    implementation 'com.squareup.okhttp3:okhttp:5.0.0-alpha.14'

    // UI Helpers
    implementation 'com.github.clans:fab:1.6.4'
    implementation 'com.bignerdranch.android:recyclerview-multiselect:0.2'
    implementation 'de.psdev.licensesdialog:licensesdialog:2.2.0'
    implementation 'uk.co.jordanrobinson:android-color-picker:1.0.2'

    // Joda-Time for Android
    implementation 'net.danlew:android.joda:2.13.0'

    // Required for instrumented tests
    androidTestImplementation 'androidx.annotation:annotation:1.8.2'
    androidTestImplementation 'androidx.test:runner:1.6.2'

    constraints {
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.8.0") {
            because("kotlin-stdlib-jdk7 is now a part of kotlin-stdlib")
        }
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.8.0") {
            because("kotlin-stdlib-jdk8 is now a part of kotlin-stdlib")
        }
    }
}
